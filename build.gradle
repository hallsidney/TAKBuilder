plugins {
	id 'org.springframework.boot' version '2.7.5'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
//	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

task installFrontend(type: Exec){
	inputs.file('frontend/yarn.lock')
	inputs.file('frontend/package.json')
	outputs.dir('frontend/node_modules')
	commandLine("yarn", "--cwd", "frontend", "install")
}

task buildFrontend(type: Exec){
	dependsOn("installFrontend")
	inputs.dir((file("frontend")))
	outputs.dir(file("frontend/build"))
	commandLine("yarn", "--cwd", "frontend", "build")
}

task copyFrontend(type: Sync){
	dependsOn("buildFrontend")
	from(file("./frontend/build"))
	into(file("$buildDir/resources/main/static"))
	doLast {
		println("copied build frontend to static resources")
	}
}

task cleanFrontend(type: Delete) {
	delete(file("./frontend/build"))
	delete(file("./src/main/resources/static"))
}

bootRun{
	dependsOn(copyFrontend)
}

clean {
	dependsOn(cleanFrontend)
}